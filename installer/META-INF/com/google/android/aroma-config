### LICENSE:
#
# Copyright (C) 2011 Ahmad Amarullah ( http://amarullz.com/ )
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
### FILE INFO:
#
# AROMA Installer 2.70 - AROMA Installer Test Script Configuration
#       (c) 2011-2012 by Ahmad Amarullah
#           amarullz - xda-developers
#           http://www.amarullz.com/
#
#       Binary Version  : 2.70B6
#       Binary Codename : Flamboyan
#
### INFO:
#
# * AROMA Resource Dir  = META-INF/com/google/android/aroma
#
# * AROMA Temporary Dir = /tmp/aroma/

ini_set("force_colorspace","rgba");

ini_set("rom_name",             "KofilaKernel");
ini_set("rom_version",          "3.0");
ini_set("rom_author",           "patrik.KT");
ini_set("rom_device",           "HTC Desire 601");
ini_set("rom_date",             "October 18, 2014");


ini_set("dp","4");

fontresload( "0", "ttf/Roboto-Regular.ttf", "12" );
fontresload( "1", "ttf/Roboto-Regular.ttf", "18" ); 

#splash(3000, "kofila");

viewbox(
    "Welcome",
    "You are about to flash <b>"+
	  ini_get("rom_name")+
	"</b>\nfor <b>"+ini_get("rom_device")+"</b>.\n\n"+
    
	
      "  VERSION\t: <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
      "  AUTHOR \t: <b><#selectbg_g>"+ini_get("rom_author")+"</#></b>\n"+
      "  RELEASED\t: <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n\n\n"+

    "Press Next to Continue...",
    "@welcome"
);

agreebox("Terms Of Use","Please read the KofilaKernel Terms of Use...","icons/agreement",
   "" + readfile_aroma("agreement.txt"),
   "I Agree with these Terms Of Use...",
   "Please check the box..."
);

alert(
  "PLEASE NOTE:",
  "This kernel will allow you to choose hardware settings that may not work with your device. If you install this and have problems such as instability or random reboots, please set a lower CPU frequency or install back the stock kernel.",
  "@alert",
  "OK"
);

selectbox(
  "Device and ROM","","","device.prop",

  "Select your device variant","",2,
  "UL","GSM",1,
  "CL","CDMA",0,

  "Select your ROM","",2,
  "Sense 5.5","stock",1,
  "Sense 6","InsertCoin",0
);

selectbox(
  "Disk Settings","Select i/o scheduler","","disk.prop",
  "CFQ","(stock)",1,
  "NOOP","",0,
  "deadline","",0,
  "FIOPS","",0,
  "SIO","",0
);


checkbox(
  "Other settings","<b>Select the mods you want to install</b>","@personalize","mods.prop",
  "Enable USB fastcharge","",1,
  "Enable Logo2menu","Use HTC logo as menu button",1
);
  
selectbox(
  "Wake gesture settings","configure screen wake gestures","@personalize","wg.prop",
  "Wake Gestures","Wake gestures support (requires Sense 6 Toolbox or Wake Gestures Xposed module)",0,
  "Traditional S2W/DT2W","Built into kernel, nothing else required",1,
  "No screen wake gestures","",0
);

if file_getprop("/tmp/aroma/wg.prop","selected.0") == "1"
then
checkbox(
  "Wake Gesture options","More options are available in Sense 6 Toolbox or Wake Gestures Xposed module","@personalize","wgopt.prop",
  "Pocket detection","prevents accidental wakes",1
);
endif;

if file_getprop("/tmp/aroma/wg.prop","selected.0") == "2"
then
checkbox(
  "S2W/DT2W options","Select the mods you want to install","@personalize","sw.prop",
  "Sweep right","Sweep right across buttons to wake phone",1,
  "Sweep left","Sweep left across buttons to wake phone",1,
  "Sweep up","Sweep up to wake phone",1,
  "Sweep down","Sweep down to wake phone",1,
  "DoubleTap2Wake","Double tap bottom of screen to wake phone",0,
  "DoubleTap2Wake (fullscreen)","Double tap anywhere on screen to wake phone",1,
  "Logo2Wake","Wake phone with long press of HTC logo",0,
  "Pocket detection","prevents accidental wakes",1
);

if file_getprop("/tmp/aroma/sw.prop","item.0.5") == "1" && file_getprop("/tmp/aroma/sw.prop","item.0.6") == "1"
then 
alert(
  "PLEASE NOTE:",
  "You must choose either doubletap2wake or doubletap2wake fullscreen.  You can't choose both",
  "@alert",
  "OK"
);
back("1");
endif;

if file_getprop("/tmp/aroma/sw.prop","item.0.1") == "1" ||
  file_getprop("/tmp/aroma/sw.prop","item.0.2") == "1" ||
  file_getprop("/tmp/aroma/sw.prop","item.0.3") == "1" ||
  file_getprop("/tmp/aroma/sw.prop","item.0.4") == "1" ||
  file_getprop("/tmp/aroma/sw.prop","item.0.5") == "1" ||
  file_getprop("/tmp/aroma/sw.prop","item.0.6") == "1" ||
  file_getprop("/tmp/aroma/sw.prop","item.0.7") == "1"
then
selectbox(
  "Gesture vibration settings","Set the amount of vibration for sweep2wake/logo2wake/doubletap2wake\n","@personalize","vibrate.prop",
  "No vibration","",0,
  "Default vibration","",1,
  "Stronger vibration","",0
);
endif;
endif;


selectbox(
  "Sleep Gesture Settings","","","s2s.prop",

  "Sweep2Sleep","Sweep across buttons to put phone to sleep","2",
  "Disable Sweep2Sleep","",0,
  "Sweep right","",0,
  "Sweep left","(default)",1,
  "Sweep left or right","",0,

  "Logo2Sleep","Press HTC logo to put phone to sleep","2",
  "Enable Logo2Sleep","",0,
  "Disable Logo2Sleep","(also disables Logo2Wake",0
);

menubox(
  "KofilaKernel Installer",
  "Ready to install",
  "@info",
  "menu.prop",
  "Install KofilaKernel","","@install",
  "Abort installation","","@default"
);

if prop("menu.prop","selected")=="2"
then
  exit("");
endif;

setvar("retstatus",
install(
  "Installing",
  "Installing KofilaKernel...\nPlease wait...",
  "@install")
);

ini_set("text_next", "Finish");

if getvar("retstatus")=="0" then
  viewbox(
  "Installation Completed",
  "<#080>Congratulations...</#>\n\n"+
  "KofilaKernel has been installed successfully!\n\n"+
  "Please let your phone boot completely...\n\n"+
  "Enjoy!",
  "@info"
);
else
   viewbox("Installation Failed","\n\nInstaller Status: "+ getvar("retstatus"),"@alert");
endif;
